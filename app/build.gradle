apply plugin: 'com.android.application'

apply plugin: 'kotlin-android'

apply plugin: 'kotlin-android-extensions'

apply plugin: 'kotlin-kapt'

apply plugin: 'jacoco'

androidExtensions {
    experimental = true
}

def appCenterKey = System.getenv("APP_CENTER_KEY") ?: app_center_key
def adMobId = System.getenv('AD_MOB_ID') ?: ad_mob_id
def adUnitId = System.getenv('AD_UNIT_ID') ?: ad_unit_id
def buildVersionCode = 1

android {
    compileSdkVersion 27
    defaultConfig {
        applicationId "com.cameronvwilliams.raise"
        minSdkVersion 21
        targetSdkVersion 27
        versionCode buildVersionCode
        versionName "0.1.1"
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
        multiDexEnabled true
        buildConfigField "String", "APP_CENTER_KEY", "\"${appCenterKey}\""
        buildConfigField "String", "ADMOB_APP_ID", "\"${adMobId}\""
        buildConfigField "String", "AD_UNIT_ID", "\"${adUnitId}\""
    }
    buildTypes {
        debug {
            applicationIdSuffix ".debug"
            testCoverageEnabled true
            minifyEnabled false
            shrinkResources false
            debuggable true
        }
        release {
            minifyEnabled true
            shrinkResources true
            debuggable false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
    flavorDimensions "remote"
    productFlavors {
        local {
            dimension "remote"
            buildConfigField "String", "SOCKET_URL", '"http://10.0.2.2:3000/"'
            buildConfigField "String", "API_URL", '"http://10.0.2.2:3000/api/"'
            buildConfigField "String", "BASE_URL", '"http://10.0.2.2:3000/"'
            resValue 'string', 'DEEPLINK_URL', '10.0.2.2:3000'
        }
        staging {
            dimension "remote"
            buildConfigField "String", "SOCKET_URL", '"https://raise.cameronvwilliams.com/"'
            buildConfigField "String", "API_URL", '"https://raise.cameronvwilliams.com/api/"'
            buildConfigField "String", "BASE_URL", '"https://raise.cameronvwilliams.com/"'
            resValue 'string', 'DEEPLINK_URL', 'raise.cameronvwilliams.com'
        }
        mock {
            dimension "remote"
            buildConfigField "String", "SOCKET_URL", '"http://localhost/"'
            buildConfigField "String", "API_URL", '"http://localhost/api/"'
            buildConfigField "String", "BASE_URL", '"http://localhost/"'
            resValue 'string', 'DEEPLINK_URL', 'localhost'
        }
    }
    sourceSets {
        debug {
            java.srcDirs = ['src/debug/java']
            manifest.srcFile 'src/debug/AndroidManifest.xml'
        }
        mock {
            java.srcDirs = ['src/mock/java']
        }
        mockDebug {
            java.srcDirs = ['src/mockDebug/java']
        }
    }
    testOptions {
        unitTests {
            all {
                jacoco {
                    includeNoLocationClasses = true
                }
            }
            includeAndroidResources = true
        }
    }
}

jacoco {
    toolVersion = "0.7.9"
}

dependencies {
    def retrofitVersion = '2.4.0'
    def okhttpVersion = '3.10.0'
    def supportVersion = '27.1.1'
    def playVisionVersion = '15.0.2'
    def fbCoreVersion = '16.0.0'
    def fbAdsVersion = '15.0.1'
    def gsonVersion = '2.8.2'
    def daggerVersion = '2.16'
    def rxjavaVersion = '2.1.5'
    def rxAndroidVersion = '2.0.1'
    def socketIOVersion = '1.0.0'
    def timberVersion = '4.6.0'
    def appCenterSdkVersion = '1.5.1'
    def permissionDispatcherVersion = '3.1.0'

    //Support Libraries
    implementation "com.android.support:appcompat-v7:$supportVersion"
    implementation "com.android.support:customtabs:$supportVersion"
    implementation "com.android.support:recyclerview-v7:$supportVersion"
    implementation "com.android.support:design:$supportVersion"
    implementation "com.android.support:support-fragment:$supportVersion"
    implementation 'com.android.support.constraint:constraint-layout:1.1.1'

    //Play Services Libraries
    implementation "com.google.android.gms:play-services-vision:$playVisionVersion"

    // Firebase
    implementation "com.google.firebase:firebase-core:$fbCoreVersion"
    implementation "com.google.firebase:firebase-ads:$fbAdsVersion"

    // App Center
    implementation "com.microsoft.appcenter:appcenter-distribute:$appCenterSdkVersion"
    implementation "com.microsoft.appcenter:appcenter-analytics:$appCenterSdkVersion"
    implementation "com.microsoft.appcenter:appcenter-crashes:$appCenterSdkVersion"

    //Retrofit
    implementation "com.squareup.retrofit2:retrofit:$retrofitVersion"
    implementation "com.squareup.retrofit2:converter-gson:$retrofitVersion"
    implementation "com.squareup.retrofit2:adapter-rxjava2:$retrofitVersion"

    //OkHttp
    implementation "com.squareup.okhttp3:okhttp:$okhttpVersion"

    //Kotlin
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlinVersion"

    //Gson
    implementation "com.google.code.gson:gson:$gsonVersion"

    //Dagger
    implementation "com.google.dagger:dagger-android-support:$daggerVersion"
    kapt "com.google.dagger:dagger-compiler:$daggerVersion"
    kapt "com.google.dagger:dagger-android-processor:$daggerVersion"

    //RxJava
    implementation "io.reactivex.rxjava2:rxjava:$rxjavaVersion"
    implementation "io.reactivex.rxjava2:rxandroid:$rxAndroidVersion"
    implementation 'io.reactivex.rxjava2:rxkotlin:2.2.0'

    // Socket IO
    implementation("io.socket:socket.io-client:$socketIOVersion") {
        exclude group: 'org.json', module: 'json'
    }

    //Timber
    implementation "com.jakewharton.timber:timber:$timberVersion"

    // Permission Dispatcher
    implementation "com.github.hotchemi:permissionsdispatcher:$permissionDispatcherVersion"
    kapt "com.github.hotchemi:permissionsdispatcher-processor:$permissionDispatcherVersion"

    implementation 'com.jakewharton.rxbinding2:rxbinding-kotlin:2.1.1'
    implementation 'com.jakewharton.rxbinding2:rxbinding-support-v4:2.1.1'
    implementation 'com.jakewharton.rxbinding2:rxbinding-appcompat-v7-kotlin:2.1.1'
    implementation 'com.jakewharton.rxbinding2:rxbinding-recyclerview-v7:2.1.1'
    implementation 'com.jakewharton.rxbinding2:rxbinding-design:2.1.1'

    // Deeplink Dispatch
    implementation 'com.airbnb:deeplinkdispatch:3.1.1'
    kapt 'com.airbnb:deeplinkdispatch-processor:3.1.1'

    //Testing Libs
    testImplementation 'junit:junit:4.12'
    testImplementation 'org.mockito:mockito-core:2.18.3'
    testImplementation 'org.robolectric:robolectric:3.6.1'
    androidTestImplementation('com.android.support.test.espresso:espresso-core:2.2.2', {
        exclude group: 'com.android.support', module: 'support-annotations'
        exclude group: 'com.google.code.findbugs'
    })
    androidTestImplementation "com.android.support.test.espresso:espresso-intents:2.2.2"
    debugImplementation 'com.facebook.stetho:stetho:1.5.0'
    debugImplementation 'com.facebook.stetho:stetho-okhttp3:1.5.0'
}

task copyTestClasses(type: Copy) {
    from "build/tmp/kotlin-classes/debugUnitTest"
    into "build/intermediates/classes/debug"
}

task copySdkClasses(type: Copy) {
    from "build/tmp/kotlin-classes/debug"
    into "build/intermediates/classes/debug"
}

task jacocoTestReport(type: JacocoReport, dependsOn: 'testDebugUnitTest') {
    group = 'Reporting'
    description = 'Generate Jacoco coverage reports'
    reports {
        xml.enabled = true
        html.enabled = true
    }
    def excludes = [
            '**/R.class', '**/R$*.class', '**/BuildConfig.*', '**/Manifest*.*',
            'android/**/*.*', '**/*Builder.*', '**/*_MembersInjector.class',
            '**/*_MembersInjector*.*', '**/*_*Factory*.*', '**/*Component*.*',
            '**/*Module*.*', '**/R.class', '**/R$*.class', '**/*$ViewInjector*.*',
            '**/BuildConfig.*', '**/Manifest*.*', '**/*Test*.*', 'android/**/*.*',
            '**/*Fragment.*', '**/*Activity.*'
    ]
    // generated classes
    classDirectories = fileTree(
            dir: "$buildDir/intermediates/classes/debug",
            excludes: excludes
    ) + fileTree(
            dir: "$buildDir/tmp/kotlin-classes/debug",
            excludes: excludes
    )

    // sources
    sourceDirectories = files([
            android.sourceSets.main.java.srcDirs,
            "src/main/kotlin"
    ])
    executionData = files("$buildDir/jacoco/testDebugUnitTest.exec")
}

apply plugin: 'com.google.gms.google-services'
